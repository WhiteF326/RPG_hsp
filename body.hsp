randomize
#include "hspmath.as"
#const windowMulti 150
#const windowSize 480 * windowMulti / 100
#module
	; 結果が大きくなりやすいイカサマ乱数
	#defcfunc bigrnd int rndmax, int qCof
		if(rndmax = 1) : return 1
		dim ans
		repeat rndmax - 1
			if rnd(qCof) > 1 : ans++
		loop
		return ans
#global

*init
	;マップチップ読み込み
	buffer 32
	picload "Chips/MapChip.png", 32

	;メインウィンドウ読み込み
	screen 0, windowSize, windowSize, 0

	;汎用変数
	dim allVector, 8, 2					;上下左右斜め8方向のベクトル x,y
	allVector(0, 0) = -1 : allVector(0, 1) = -1
	allVector(1, 0) = 0 : allVector(1, 1) = -1
	allVector(2, 0) = 1 : allVector(2, 1) = -1
	allVector(3, 0) = -1 : allVector(3, 1) = 0
	allVector(4, 0) = 1 : allVector(4, 1) = 0
	allVector(5, 0) = -1 : allVector(5, 1) = 1
	allVector(6, 0) = 0 : allVector(6, 1) = 1
	allVector(7, 0) = 1 : allVector(7, 1) = 1


	;マップ変数用意
	dim mSize : mSize = 24
	dim frq : frq = 4				; mSize / frq > 3 && frq > 4
	dim chipSize : chipSize = windowSize / mSize
	dim map, mSize, mSize
	sdim buf
	notesel buf : noteload "Chips/ChipPoint.txt"
	dim mapPoints, notemax
	repeat notemax
		sdim t : noteget t, cnt
		mapPoints(cnt) = int(t)
	loop

	;プレイヤー変数用意
	dim playerX
	dim playerY

	;マップチップ変数
	noteload "Chara/Order.txt"
	dim charCnt : charCnt = notemax
	repeat notemax, 1
		screen cnt
		sdim fn : noteget fn, cnt - 1
		celload "Chara/" + fn, cnt
		celdiv cnt, 20, 20
	loop

	;マップ詳細描画用変数
	noteload "Chips/floorVectors.txt"
	sdim mapDetails, 16, 40
	repeat 40
		noteget mapDetails(cnt), cnt
	loop

	;マップその他宣言
	buffer 6, windowSize, windowSize, 0

	;アニメーション番号取得
	notesel buf : noteload "Chara/AnimList.csv"
	sdim anim, 16, 8, 4
	repeat 8
		sdim fn : noteget fn, cnt
		split fn, ",", anim(cnt, 0), anim(cnt, 1), anim(cnt, 2), anim(cnt, 3)
	loop

	;アニメーションその他宣言
	dim pAnimNo : pAnimNo = 0
	buffer 5, windowSize, windowSize, 0 : cls 4
	gsel 0

*main
	gosub *mapGenerate
	gosub *mapPaint

	repeat -1
		gosub *playerPrint
		gosub *allPrint

		wait 10
		pAnimNo = (pAnimNo + 1) \ 4
	loop
	stop

*mapGenerate
	//縦と横にfrq回数仕切りを作成する
	dim xDivPoint, frq + 2 : dim yDivPoint, frq + 2
	xDivPoint(frq + 1) = mSize : yDivPoint(frq + 1) = mSize
	repeat frq, 1
		xDivPoint(cnt) = xDivPoint(cnt - 1) + bigrnd(mSize / frq - 1, 14) + 2
		if(xDivPoint(cnt) < 2) : xDivPoint(cnt) = 2
		if(xDivPoint(cnt) > mSize - 2) : xDivPoint(cnt) = mSize - 2
		yDivPoint(cnt) = yDivPoint(cnt - 1) + bigrnd(mSize / frq - 1, 14) + 2
		if(yDivPoint(cnt) < 2) : yDivPoint(cnt) = 2
		if(yDivPoint(cnt) > mSize - 2) : yDivPoint(cnt) = mSize - 2
	loop

	//小部屋の生成
	dim roomsInfo, int(pow(frq + 1, 2)), 4
	repeat length(xDivPoint) - 1
		dim xPtr : xPtr = cnt
		repeat length(yDivPoint) - 1
			dim targetRoom : targetRoom = xPtr * (frq + 1) + cnt
			//左端と上端を決定する
			if xDivPoint(xPtr + 1) - xDivPoint(xPtr) = 2 : roomsInfo(targetRoom, 0) = xDivPoint(xPtr) + 1 : else{
				roomsInfo(targetRoom, 0) = rnd(xDivPoint(xPtr + 1) - xDivPoint(xPtr) - 2) + xDivPoint(xPtr) + 1
			}
			;roomsInfo(targetRoom, 0) = rnd(xDivPoint(xPtr + 1) - xDivPoint(xPtr) - 2) + xDivPoint(xPtr) + 1
			if yDivPoint(cnt + 1) - yDivPoint(cnt) = 2 : roomsInfo(targetRoom, 2) = yDivPoint(cnt) + 1 : else{
				roomsInfo(targetRoom, 1) = rnd(yDivPoint(cnt + 1) - yDivPoint(cnt) - 2) + yDivPoint(cnt) + 1
			}
			;roomsInfo(targetRoom, 1) = rnd(yDivPoint(cnt + 1) - yDivPoint(cnt) - 2) + yDivPoint(cnt) + 1
			//右端と下端を決定する
			if roomsInfo(targetRoom, 0) = xDivPoint(xPtr + 1) - 1 : roomsInfo(targetRoom, 2) = 1: else{
				roomsInfo(targetRoom, 2) = bigrnd(xDivPoint(xPtr + 1) - roomsInfo(targetRoom, 0), 10) + 1
			}
			if roomsInfo(targetRoom, 1) = yDivPoint(cnt + 1) - 1 : roomsInfo(targetRoom, 3) = 1: else{
				roomsInfo(targetRoom, 3) = bigrnd(yDivPoint(cnt + 1) - roomsInfo(targetRoom, 1), 10) + 1
			}
		loop
	loop
	//実際の埋め立て
	repeat length(roomsInfo)
		targetRoom = cnt
		repeat roomsInfo(targetRoom, 2)
			dim x : x = cnt
			repeat roomsInfo(targetRoom, 3)
				map(x + roomsInfo(targetRoom, 0), cnt + roomsInfo(targetRoom, 1)) = 1
			loop
		loop
	loop

	//通路の生成
	repeat frq + 1
		dim xRoot : xRoot = cnt
		repeat frq + 1
			dim yRoot : yRoot = cnt
			targetRoom = xRoot * frq + yRoot
			//上へ通路を作る
			if (yRoot > 0) & (roomsInfo(targetRoom, 1) + roomsInfo(targetRoom, 3) > yDivPoint(yRoot)){
				dim xRootPoint : xRootPoint = rnd(roomsInfo(targetRoom, 2)) + roomsInfo(targetRoom, 0)
				repeat roomsInfo(targetRoom, 1) + roomsInfo(targetRoom, 3) - yDivPoint(yRoot)
					map(xRootPoint, cnt + yDivPoint(yRoot)) = 1
				loop
			}
			//左へ通路を作る
			if (xRoot > 0) & (roomsInfo(targetRoom, 0) + roomsInfo(targetRoom, 2) > xDivPoint(xRoot)){
				dim yRootPoint : yRootPoint = rnd(roomsInfo(targetRoom, 3)) + roomsInfo(targetRoom, 1)
				repeat (roomsInfo(targetRoom, 0) + roomsInfo(targetRoom, 2)) - xDivPoint(xRoot)
					map(cnt + xDivPoint(xRoot), yRootPoint) = 1
				loop
			}
			//右へ通路を作る
			if (targetRoom / (frq + 1) < frq) & (xDivPoint(xRoot + 1) > (roomsInfo(targetRoom, 0) + roomsInfo(targetRoom, 2))){
				dim yRootPoint : yRootPoint = rnd(roomsInfo(targetRoom, 3)) + roomsInfo(targetRoom, 1)
				repeat xDivPoint(xRoot + 1) - (roomsInfo(targetRoom, 0) + roomsInfo(targetRoom, 2))
					map(cnt + roomsInfo(targetRoom, 0) + roomsInfo(targetRoom, 2), yRootPoint) = 1
				loop
			}
			//下へ通路を作る
			if (targetRoom \ (frq + 1) < frq) & (yDivPoint(yRoot + 1) > (roomsInfo(targetRoom, 1) + roomsInfo(targetRoom, 3))){
				dim xRootPoint : xRootPoint = rnd(roomsInfo(targetRoom, 2)) + roomsInfo(targetRoom, 0)
				repeat yDivPoint(yRoot + 1) - (roomsInfo(targetRoom, 1) + roomsInfo(targetRoom, 3))
					map(xRootPoint, cnt + roomsInfo(targetRoom, 1) + roomsInfo(targetRoom, 3)) = 1
				loop
			}
		loop
	loop

	//仕切り部で通路を繋ぐ
	repeat frq, 1
		dim select : select = cnt
		dim tmp : tmp = 0
		repeat mSize
			if map(cnt, xDivPoint(select)) = 1 : tmp = (tmp + 1) \ 2
			if tmp = 1 : map(cnt, xDivPoint(select)) = 1
		loop
		tmp = 0
		repeat mSize
			if map(yDivPoint(select), cnt) = 1 : tmp = (tmp + 1) \ 2
			if tmp = 1 : map(yDivPoint(select), cnt) = 1
		loop
	loop

	//自分の出現位置を設定する
	repeat -1
		playerX = rnd(mSize)
		playerY = rnd(mSize)
		if map(playerX, playerY) = 1 : break
	loop

	return

*mapPaint
	gsel 6
	gmode 2
	repeat mSize
		x = cnt
		repeat mSize
			if(map(x, cnt) != 0){
				y = cnt
				pos x * chipSize, y * chipSize
				dim chipNo : chipNo = mapPoints(map(x, y))
				sdim checker, 16
				repeat 8
					if (x + allVector(cnt, 0) < 0) || (x + allVector(cnt, 0) >= mSize) || (y + allVector(cnt, 1) < 0) || (y + allVector(cnt, 1) >= mSize) {
						checker += "1"
					}else{
						checker += str((map(x + allVector(cnt, 0), y + allVector(cnt, 1)) + 1) \ 2)
					}
				loop
				repeat 40
					if checker = mapDetails(cnt){
						chipNo += cnt : break
					}
				loop
				gzoom chipSize, chipSize, 32, (chipNo \ 8) * 16, (chipNo / 8) * 16, 16, 16
				wait 10
			}
		loop
	loop
	return

*playerPrint
	//一時的に下向きだけ
	gsel 5
	color 0, 0, 0 : boxf
	dim charVector : charVector = 4
	pos chipSize * playerX, chipSize * playerY
	celput 1, int(anim(charVector, pAnimNo)), double(chipSize) / double(20), double(chipSize) / double(20)
	return

*allPrint
	redraw 0
	gsel 0 : gmode 2
	pos 0, 0 : gcopy 6, 0, 0, windowSize, windowSize
	pos 0, 0 : gcopy 5, 0, 0, windowSize, windowSize
	redraw 1
	return
